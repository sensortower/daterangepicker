class Period
  @allPeriods: [
    'day'
    'week'
    'month'
    'quarter'
    'year'
  ]

  @scale: (period) ->
    if period in ['day', 'week'] then 'month' else 'year'

  @showWeekDayNames: (period) ->
    if period in ['day', 'week'] then true else false

  @nextPageArguments: (period) ->
    amount = if period == 'year' then 9 else 1
    scale = @scale(period)
    [amount, scale]

  @format: (period) ->
    switch period
      when 'day', 'week'
        'D'
      when 'month'
        'MMM'
      when 'quarter'
        '\\QQ'
      when 'year'
        'YYYY'

  @title: (period, localeObj) ->
    switch period
      when 'day'
        localeObj.dayLabel
      when 'week'
        localeObj.weekLabel
      when 'month'
        localeObj.monthLabel
      when 'quarter'
        localeObj.quarterLabel
      when 'year'
        localeObj.yearLabel

  @dimentions: (period) ->
    switch period
      when 'day'
        [7, 6]
      when 'week'
        [1, 6]
      when 'month'
        [3, 4]
      when 'quarter'
        [2, 2]
      when 'year'
        [3, 3]

  @methods: [
    'scale',
    'showWeekDayNames',
    'nextPageArguments',
    'format',
    'title',
    'dimentions'
  ]

  @extendObservable: (observable) ->
    @methods.forEach (method) ->
      observable[method] = -> Period[method](observable())
    observable
